<&|/Widgets/TitleBox,
    title => loc("Calendar"),
    title_href => "Search/Calendar.html" &>

<table class="rtxcalendar">
<thead>
<tr>
% my $date = $begin->clone;
% while ( $date <= $end ) {
<th width="14%"><%$rtdate->GetWeekday($date->day_of_week % 7)%></th>
% $date = $set->next($date);
% }
</tr>
</thead>
<tbody>
<tr>
% $date = $begin->clone;
% while ($date <= $end) {
%   my @DayTickets   = grep { $_->DueObj->Date eq $date->strftime("%F") } @Tickets;
%   my @DayReminders = grep { $_->DueObj->Date eq $date->strftime("%F") } @Reminders;
<td>
<p class="date"><%$date->day%></p>
% for my $t (@DayTickets) {
<& /Elements/CalendarEvent, Object => $t, Date => $date &>
% }
% for my $t (@DayReminders) {
<& /Elements/CalendarEvent, Object => $t, Date => $date, Reminder => 1 &>
% }
</td>
% $date = $set->next($date);
% }
</tr>
</tbody>
</table>

 </&>

<%INIT>

use RTx::Calendar;

my $title = loc("Calendar");

my $rtdate = RT::Date->new($session{'CurrentUser'});

my $today = DateTime->today;
my $begin = DateTime->today->subtract( days => 3);
my $end   = DateTime->today->add( days => 3);

# use this to loop over days until $end
my $set = DateTime::Set->from_recurrence(
    next => sub { $_[0]->truncate( to => 'day' )->add( days => 1 ) }
);

my %already_seen;
my @Tickets;

my $Tickets = RT::Tickets->new($session{'CurrentUser'});
$Tickets->LimitStatus( VALUE => 'open' );
$Tickets->LimitStatus ( VALUE => 'new');
$Tickets->LimitStatus ( VALUE => 'stalled');

$Tickets->LimitOwner(VALUE => $session{'CurrentUser'}->Id);
$Tickets->LimitOwner(VALUE => 'Nobody');

$Tickets->LimitDue(OPERATOR => ">=", VALUE => $begin->strftime("%F") );
$Tickets->LimitDue(OPERATOR => "<=", VALUE => $end->strftime("%F") );

while ( my $Ticket = $Tickets->Next()) {
  push @Tickets, $Ticket
    unless $already_seen{$Ticket->Id}++;
}

my $Reminders = RT::Tickets->new($session{'CurrentUser'});
$Reminders->FromSQL('(Owner = "Nobody" OR Owner = "'.$session{'CurrentUser'}->Name.'")' .
                    ' AND Type = "reminder" AND (Status = "new" OR Status = "open")' .
                    ' AND Due >= "' . $begin->strftime("%F") . '"' .
                    ' AND Due <= "' . $end->strftime("%F") . '"');

$Reminders->OrderBy(FIELD => 'Due', ORDER => 'ASC');

my @Reminders;
while ( my $Reminder = $Reminders->Next() ) {
  # only show reminder refering to a ticket
  push @Reminders, $Reminder
    if $Reminder->RefersTo->First
}


</%INIT>
